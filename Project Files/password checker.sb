DefineASCIIBounds()

TextWindow.Title = "Password Strength Checker"
TextWindow.BackgroundColor = "DarkBlue"
TextWindow.ForegroundColor = "Cyan"
TextWindow.Clear()

TextWindow.WriteLine("Type in a good password:")
pass = TextWindow.Read()

CheckPassStrength()
JudgePassStrength()

TextWindow.ForegroundColor = "Gray"
TextWindow.Write(LF + "Password " + QT + pass + QT)
TextWindow.WriteLine(" is " + verdict + "!" + LF)

TextWindow.ForegroundColor = "Yellow"
TextWindow.WriteLine("Upper  Letters ->  " + uppers)
TextWindow.WriteLine("Lower  Letters ->  " + lowers)
TextWindow.WriteLine("Number Digits  ->  " + digits)
TextWindow.WriteLine("Other  Chars   ->  " + others)

TextWindow.ForegroundColor = "Magenta"
TextWindow.WriteLine(LF)

Sub DefineASCIIBounds
  
  UPPER_A = Text.GetCharacterCode("A")
  UPPER_Z = Text.GetCharacterCode("Z")
  
  LOWER_A = Text.GetCharacterCode("a")
  LOWER_Z = Text.GetCharacterCode("z")
  
  NUM_0 = Text.GetCharacterCode(0)
  NUM_9 = Text.GetCharacterCode(9)
  
  LF = Text.GetCharacter(10)
  TB = Text.GetCharacter(9)
  QT = Text.GetCharacter(34)
  
EndSub

Sub CheckPassStrength
  
  uppers = 0
  lowers = 0
  digits = 0
  others = 0
  
  len = Text.GetLength(pass)
  
  For i = 1 To len
    code = Text.GetCharacterCode( Text.GetSubText(pass  i, 1) )
    
    If code >= UPPER_A And code <= UPPER_Z Then
      uppers = uppers + 1
    ElseIf code >= LOWER_A And code <= LOWER_Z Then
      lowers = lowers + 1
    ElseIf code >= NUM_0 And code <= NUM_9 Then
      digits = digits + 1
    Else
      others = others + 1
    EndIf
    
  EndFor
  
EndSub

Sub JudgePassStrength
  
  strength = 0
  
  If uppers > 0 Then
    strength = strength + 1
  EndIf
  
  If lowers > 0 Then
    strength = strength + 1
  EndIf
  
  If digits > 0 Then
    strength = strength + 1
  EndIf
  
  If others > 0 Then
    strength = strength + 1
  EndIf
  
  If strength = 0 Then
    verdict = "EMPTY"
  ElseIf strength = 1 Then
    verdict = "WEAK"
  ElseIf strength = 2 Then
    verdict = "MEDIUM"
  ElseIf strength = 3 Then
    verdict = "STRONG"
  Else
    verdict = "MIGHTY"
  EndIf
  
EndSub